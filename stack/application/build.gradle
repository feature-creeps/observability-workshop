plugins {
    id "io.spring.dependency-management" version "1.1.0"
}

subprojects {
    group = 'feature-creeps'
    version = null

    apply plugin: 'groovy'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:2020.0.6"
            mavenBom "org.springframework.boot:spring-boot-dependencies:2.4.13"
        }
    }
    task allDeps(type: DependencyReportTask) {}
    dependencies {
        // spring
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.boot:spring-boot-starter-data-rest'

        // commons
        implementation 'commons-io:commons-io:2.11.0'
        implementation 'org.apache.commons:commons-lang3:3.12.0'

        // logs
        implementation 'net.logstash.logback:logstash-logback-encoder:4.11'
        implementation 'ch.qos.logback:logback-classic:1.2.11'
        implementation 'ch.qos.logback:logback-core:1.2.11'
        implementation 'org.apache.logging.log4j:log4j-core:2.20.0'

        // metrics
        implementation 'io.micrometer:micrometer-core'
        implementation 'io.micrometer:micrometer-registry-prometheus'
        // traces
        implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
        implementation 'org.springframework.cloud:spring-cloud-sleuth-zipkin'

        // events
        implementation 'io.honeycomb.beeline:beeline-spring-boot-starter:1.7.1'

        // todo: remove this dep
        implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:2.1.2'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        // tests still using junit4
        testImplementation("org.junit.vintage:junit-vintage-engine") {
            exclude group: "org.hamcrest", module: "hamcrest-core"
        }
    }

}

task resolveDependencies {
    doLast {
        project.rootProject.allprojects.each { subProject ->
            subProject.buildscript.configurations.each { configuration ->
                resolveConfiguration(configuration)
            }
            subProject.configurations.each { configuration ->
                resolveConfiguration(configuration)
            }
        }
    }
}

void resolveConfiguration(configuration) {
    if (isResolveableConfiguration(configuration)) {
        configuration.resolve()
    }
}

boolean isResolveableConfiguration(configuration) {
    def nonResolveableConfigurations = ['api', 'apiElements', 'compileOnly', 'compileOnlyApi', 
                                        'implementation', 'integrationTestCompileOnly', 'integrationTestImplementation',
                                        'integrationTestRuntimeOnly', 'mainSourceElements',
                                        'runtimeElements', 'runtimeOnly',
                                        'testCompileOnly', 'testImplementation', 'testResultsElementsForTest',
                                        'testRuntimeOnly',
                                        'generatedImplementation', 'generatedRuntimeOnly']

    if (nonResolveableConfigurations.contains(configuration.getName())) {
        return false
    }
    return true
}